#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Stage 3 Boss: Raika (Spell 1)"]
#Text[""]

let count = 0;
let bossObj;
let bossX = 0;
let bossY = 0;
let speed;
let imgBoss = GetCurrentScriptDirectory ~ "Yuyuko.png";

#include "script/default_system/Default_ShotConst.txt"
#include "script/default_system/Default_Effect.txt"

@Initialize {

	SetAutoDeleteObject(true);

	bossObj = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(bossObj);
	ObjMove_SetPosition(bossObj,192,-100);

	ObjMove_SetDestAtSpeed(bossObj,192,100,5);
	
	mainTask;
}

@Event {

	alternative(GetEventType())
	case(EV_REQUEST_LIFE) {
		SetScriptResult(3600);
	}
	case(EV_REQUEST_TIMER) {
		SetScriptResult(60);
	}
}

@MainLoop {

	bossX = ObjMove_GetX(bossObj);
	bossY = ObjMove_GetY(bossObj);

	ObjEnemy_SetIntersectionCircleToShot(bossObj,bossX,bossY,24);
	ObjEnemy_SetIntersectionCircleToPlayer(bossObj,bossX,bossY,32);

	yield;

}

@Finalize {

}

function wait(w) { loop(w) {yield;} }

task mainTask {
	renderBoss;
	fire;
	scriptEnd;
}

task renderBoss {

	let dir;
	let speed;

	ObjPrim_SetTexture(bossObj,imgBoss);
	ObjSprite2D_SetSourceRect(bossObj,0,0,224,200);
	ObjSprite2D_SetDestCenter(bossObj);
	ObjRender_SetScaleXYZ(bossObj,0.4,0.4,0);

	while(!Obj_IsDeleted(bossObj)) {

		dir = ObjMove_GetAngle(bossObj);
		speed = ObjMove_GetSpeed(bossObj);

		if (speed == 0) { ObjSprite2D_SetSourceRect(bossObj,0,0,224,200); ObjRender_SetAngleXYZ(bossObj,0,0,0); }
		else if (cos(dir) < 0) {ObjSprite2D_SetSourceRect(bossObj,0,0,224,200); ObjRender_SetAngleXYZ(bossObj,0,0,0); }
		else if (cos(dir) > 0) {ObjSprite2D_SetSourceRect(bossObj,0,0,224,200); ObjRender_SetAngleXYZ(bossObj,0,180,0); }
		yield;
	}

}

function angleToPlayer {

	let dir = atan2(GetPlayerY-bossY,GetPlayerX-bossX);
	return dir

}

task fire {
	wait(60);
	let rev = 1;
	loop {
		loop(2) {
			encaseShots(rev);
			rev = rev * -1;
			
		}
		wait(120);
	}
}

task encaseShots(reverse) {
	ascent(h in 0..60) {
		ascent(i in 0..4) {
			let obj = CreateShotA1(ObjMove_GetX(bossObj),ObjMove_GetY(bossObj),6,GetAngleToPlayer(bossObj)+i*(180/4)*reverse,61,30);
			objEdit(obj,reverse);
		}
	yield;
	}
	ObjMove_SetDestAtFrame(bossObj,GetStgFrameWidth/2+rand(-150,150),GetStgFrameHeight/4+rand(-50,50),60);
}

task objEdit(obj,reverse) {
	let ascentCount = 0;
	let randDel = 0;
	while((ObjMove_GetX(obj) > 0) && (ObjMove_GetX(obj) < GetStgFrameWidth) && (ObjMove_GetY(obj) > 0) && (ObjMove_GetY(obj) < GetStgFrameHeight)) {
		ObjMove_SetAngularVelocity(obj,18*sin(ascentCount*6)*reverse);
		ascentCount ++;
		yield;
	}
		CreateStraightLaserA1(ObjMove_GetX(bossObj),ObjMove_GetY(bossObj),atan2(ObjMove_GetY(obj)-bossY,ObjMove_GetX(obj)-bossX),700,15,15,161,30);
		ObjMove_SetAngle(obj,GetAngleToPlayer(obj));
		ObjMove_SetAngularVelocity(obj,0);
		ObjMove_SetSpeed(obj,0);
		randDel = rand(0,2);
		if(randDel == 1) {Obj_Delete(obj);}
		while(!Obj_IsDeleted(obj)) {
			ObjMove_SetSpeed(obj,ObjMove_GetSpeed(obj)+0.02);
			yield;
		}
}

task scriptEnd {
	while(ObjEnemy_GetInfo(bossObj,INFO_LIFE) > 0) {
		yield;
	}
	Obj_Delete(bossObj);
	DeleteShotAll(TYPE_ALL,TYPE_ITEM);
	CloseScript(GetOwnScriptID);

}